(defpackage :aoc-2016-04
  (:use :cl))

(in-package :aoc-2016-04)

(defvar *input* (uiop:read-file-lines "../resources/input04"))

(defvar *example*
  '("aaaaa-bbb-z-y-x-123[abxyz]"
    "a-b-c-d-e-f-g-h-987[abcde]"
    "not-a-real-room-404[oarel]"
    "totally-real-room-200[decoy]"))

(defun parse-room (room)
  (cl-ppcre:register-groups-bind (n (#'parse-integer i) c)
      ("^([a-z-]+)-(\\d+)\\[([a-z]+)\\]$" room)
    (list n i c)))

(defun frequencies (name)
  (let ((freq (make-hash-table)))
    (loop for c across (sort (cl-ppcre:regex-replace-all "-" name "") #'char<)
          do (incf (gethash c freq 0)))
    (sort (loop for c being the hash-keys of freq
                  using (hash-value count)
                collect (cons c count))
          #'> :key #'cdr)))

(defun calculate-checksum (alist)
  (subseq (format nil "窿祜镳骘ㄣ瞟轭犰轶泔祆邈悌旦ㄤ彐躅箬殒舡汨狎ㄣ殇ㄩㄣ栳蚪＼悌＼羽徙ㄣ镤瀛汨狎ǐ盹ǐōㄣ栳颦泔溴悌狗殇捕狗┅┅ㄤ彐躅溴泸痿钺礤钺礤殇ㄦ矧磲铋窿祜镳骘徙蝻篌钺礤泔祆邈箬殒舡汨狎殇┅┅ㄤ彐躅疳螋ㄩ铕豸祜镳骘蝻镯轭轭瘐骘悌疳蝮瀛蝻镯蝻镯骘沣ㄣ犰沲灬翦汨邈塍蹴ㄦ蝈聃孱汩弩瞟麒孱篝蜷铉沣篚椹ㄤ彐躅疳螋ㄩ铕豸祜镳骘蝻镯轭轭瘐骘悌疳蝮瀛蝻镯蝻镯骘漕ㄤ邈蝙痿钺礤椹麒孱篝蜷铉漕㈩矧翳痫戾镡赍泗篝矧徵澧蝈趱蝾椹